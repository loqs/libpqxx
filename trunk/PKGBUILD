# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=7.4.1
pkgrel=1
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=('x86_64')
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('postgresql' 'python' 'xmlto' 'doxygen' 'graphviz' 'cmake')
provides=('libpqxx.so')
source=(https://github.com/jtv/libpqxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('73b2f0a0af786d6039291b60250bee577bc7ea7c10b7550ec37da448940848b7')
sha512sums=('a30a9f5ca87944922c9a3bd92d53392855ac3b2760cb7c21411caf364e443c25d1b2d4b4022e60ff7b30bc530fc3223ab3983325217d47fd7d71375c165c8c21')

prepare() {
  cd ${pkgname}-${pkgver}
}

build() {
  cmake -B build -S "${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
        -DBUILD_DOC:BOOL='ON' \
        -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir" install
  cd ${pkgname}-${pkgver}
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
